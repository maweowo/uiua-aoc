# Experimental!
~ "git: github.com/amatgil/steal-gift" ~ AoCGet

StepA ← ⍜⊡⬚1↻₁⍢(⊂⊟⊸⍜⊢-₁°⊂|≠₁°↥₁⊡⊢)¤⊢⊚⊸= # could technically be just ⊚ but due to ⊢⊚ optimisations this is 2x faster
Do‼ ← (
  ⊗⊙"v>^<"♭:^1 ⊗⊙"#.O"∩(⊜∘⊸≠@\n)°$"_\n\n_"
  /+♭×100_1⍉⊚=2 ∧⍜⟜⨬(⇌|⇌⍉|∘|⍉)^0
)

A ← Do‼⋅⍣(StepA3)∘
B ← Do‼⍣(
  ⨬(⋅⍜∩⍉≡⍜▽⬚1↻₁⍢(
      ⊂𝄐⌞⊸⊃(
        ⨬⋅[](⨬(°0|¤⊙◌|⊟⟜⍜⊣+₁|⊟⟜⍜⊣-₁)◡⊡⍜⊢-₁)≠1𝄐◡⊡
      | ⍜⊡↥₁
      )°⊂
    | ≠₀⧻
    )¤⊢⊸⊚⊸=₄
  | StepA4
  )◿₂
)◌ (≡/⊂⊏⊙[0_0 1_1 2_3 4_1])

&p $"A: _\nB: _" ⊃A B AoCGet 2024 15

┌─╴test
  ⍤⤙≍ 2028 A $ ########
             $ #..O.O.#
             $ ##@.O..#
             $ #...O..#
             $ #.#.O..#
             $ #...O..#
             $ #......#
             $ ########
             $ 
             $ <^^>>>vv<v>>v<<

  ⍤⤙≍ 10092 A $ ##########
              $ #..O..O.O#
              $ #......O.#
              $ #.OO..O.O#
              $ #..O@..O.#
              $ #O#..O...#
              $ #O..O..O.#
              $ #.OO.O.OO#
              $ #....O...#
              $ ##########
              $ 
              $ <vv>^<v^>v>^vv^v>v<>v^v<v<^vv<<<^><<><>>v<vvv<>^v^>^<<<><<v<<<v^vv^v>^
              $ vvv<<^>^v^^><<>>><>^<<><^vv^^<>vvv<>><^^v>^>vv<>v<<<<v<^v>^<^^>>>^<v<v
              $ ><>vv>v^v^<>><>>>><^^>vv>v<^^^>>v^v^<^^>v^^>v^<^v>v<>>v^v^<v>v^^<^^vv<
              $ <<v<^>>^^^^>>>v^<>vvv^><v<<<>^^^vv^<vvv>^>v<^^^^v<>^>vvvv><>>v^<<^^^^^
              $ ^><^><>>><>^^<<^^v>>><^<v>^<vv>>v>>>^v><>^v><<<<v>>v<v<v>vvv>^<><<>^><
              $ ^>><>^v<><^vvv<^^<><v<<<<<><^v<<<><<<^^<v<^^^><^>>^<v^><<<^>>^v<v^v<v^
              $ >^>>^v>vv>^<<^v<>><<><<v<<v><>v<^vv<<<>^^v^>^^>>><<^v>>v^v><^^>>^<>vv^
              $ <><^^>^^^<><vvvvv^v<v<<>^v<v>v<<^><<><<><<<^^<<<^<<>><<><^^^>^^<>^>v<>
              $ ^^>vv<^v^v<vv>^<><v<^v>^^^>>>^^vvv^>vvv<>>>^<^>>>>>^<<^v>^vvv<>^<><<v>
              $ v^^>>><<^^<>>^v^<v^vv<>v^<<>^<^v^v><^<<<><<^<v><v<>vv>>v><v^<vv<>v^<<^

  ⍤⤙≍ 9021 B $ ##########
             $ #..O..O.O#
             $ #......O.#
             $ #.OO..O.O#
             $ #..O@..O.#
             $ #O#..O...#
             $ #O..O..O.#
             $ #.OO.O.OO#
             $ #....O...#
             $ ##########
             $ 
             $ <vv>^<v^>v>^vv^v>v<>v^v<v<^vv<<<^><<><>>v<vvv<>^v^>^<<<><<v<<<v^vv^v>^
             $ vvv<<^>^v^^><<>>><>^<<><^vv^^<>vvv<>><^^v>^>vv<>v<<<<v<^v>^<^^>>>^<v<v
             $ ><>vv>v^v^<>><>>>><^^>vv>v<^^^>>v^v^<^^>v^^>v^<^v>v<>>v^v^<v>v^^<^^vv<
             $ <<v<^>>^^^^>>>v^<>vvv^><v<<<>^^^vv^<vvv>^>v<^^^^v<>^>vvvv><>>v^<<^^^^^
             $ ^><^><>>><>^^<<^^v>>><^<v>^<vv>>v>>>^v><>^v><<<<v>>v<v<v>vvv>^<><<>^><
             $ ^>><>^v<><^vvv<^^<><v<<<<<><^v<<<><<<^^<v<^^^><^>>^<v^><<<^>>^v<v^v<v^
             $ >^>>^v>vv>^<<^v<>><<><<v<<v><>v<^vv<<<>^^v^>^^>>><<^v>>v^v><^^>>^<>vv^
             $ <><^^>^^^<><vvvvv^v<v<<>^v<v>v<<^><<><<><<<^^<<<^<<>><<><^^^>^^<>^>v<>
             $ ^^>vv<^v^v<vv>^<><v<^v>^^^>>>^^vvv^>vvv<>>>^<^>>>>>^<<^v>^vvv<>^<><<v>
             $ v^^>>><<^^<>>^v^<v^vv<>v^<<>^<^v^v><^<<<><<^<v><v<>vv>>v><v^<vv<>v^<<^

└─╴
